###########################################################################
# Do the sip generation, for python bindings
###########################################################################

include_directories ( ${PYTHON_INCLUDE_PATH} )

set ( SIP_SPEC ${CMAKE_CURRENT_SOURCE_DIR}/mantidqt.sip )
set ( SIP_SRC_IN ${CMAKE_CURRENT_SOURCE_DIR}/sip_mantidqt.cpp.in )
set ( SIP_SRC ${CMAKE_CURRENT_BINARY_DIR}/sip_mantidqt.cpp )
set ( SIP_SRC_AUTO sipmantidqtpythonpart0.cpp )

# Sip needs to have a dependency on all headers within it
set ( SIP_HDRS
  ../API/inc/MantidQtAPI/PythonSystemHeader.h
  ../API/inc/MantidQtAPI/WorkspaceObserver.h
  ../API/inc/MantidQtAPI/GraphOptions.h
  ../API/inc/MantidQtAPI/AlgorithmDialog.h
  ../API/inc/MantidQtAPI/UserSubWindow.h
  ../API/inc/MantidQtAPI/InterfaceManager.h
  ../API/inc/MantidQtAPI/MantidDesktopServices.h
  ../API/inc/MantidQtAPI/MWRunFiles.h
  ../API/inc/MantidQtAPI/WidgetScrollbarDecorator.h
  ../SliceViewer/inc/MantidQtSliceViewer/SliceViewerWindow.h
  ../SliceViewer/inc/MantidQtSliceViewer/LineViewer.h
  ../SliceViewer/inc/MantidQtSliceViewer/PeaksPresenter.h
  ../SliceViewer/inc/MantidQtSliceViewer/ProxyCompositePeaksPresenter.h
  ../SliceViewer/inc/MantidQtSliceViewer/PeaksPresenter.h
  ../SliceViewer/inc/MantidQtSliceViewer/SliceViewer.h
  ../Factory/inc/MantidQtFactory/WidgetFactory.h
  ../RefDetectorViewer/inc/MantidQtRefDetectorViewer/RefIVConnections.h
  ../RefDetectorViewer/inc/MantidQtRefDetectorViewer/RefMatrixWSImageView.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/FitPropertyBrowser.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorWhiteList.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorPreprocessMap.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorProcessingAlgorithm.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorPostprocessingAlgorithm.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/QDataProcessorWidget.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorMainPresenter.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorAppendRowCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorAppendGroupCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorClearSelectedCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorCopySelectedCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorCutSelectedCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorDeleteGroupCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorDeleteRowCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorExpandCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorExportTableCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorGroupRowsCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorImportTableCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorNewTableCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorOpenTableCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorOptionsCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorPasteSelectedCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorPlotGroupCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorPlotRowCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorProcessCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorSaveTableCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorSaveTableAsCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorSeparatorCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/DataProcessorUI/DataProcessorWorkspaceCommand.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/SlitCalculator.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/InstrumentView/InstrumentWidgetTab.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/InstrumentView/InstrumentWidget.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/InstrumentView/InstrumentWidgetRenderTab.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/InstrumentView/InstrumentWidgetPickTab.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/InstrumentView/InstrumentWidgetMaskTab.h
  ../MantidWidgets/inc/MantidQtMantidWidgets/InstrumentView/InstrumentWidgetTreeTab.h
)

set( SRC_UNITY_IGNORE_FILES )

###########################################################################
# Sip generated files
###########################################################################

# The code generated by sip causes compiler warnings therefore the 
# generated file is wrapped by ${SIP_SRC} and these warnings are
# disabled. In order for VS2010 to to this correctly the second
# custom command below is required

# Flags used: 
# -e : C++ exceptions turn into python exceptions.
# -j1 : split into 1 file
# -w : enable warnings
# -o : automatic docstrings (not supported in old version < 4.10)

add_custom_command ( OUTPUT ${SIP_SRC_AUTO}
                     COMMAND ${SIP_EXECUTABLE}
                          -I ${PYQT4_SIP_DIR} ${PYQT4_SIP_FLAGS}
                          -c ${CMAKE_CURRENT_BINARY_DIR} -j1 -w 
                          -e 
                          ${SIP_SPEC}
                     DEPENDS mantidqt.sip ${SIP_HDRS}
                     COMMENT "Generating mantidqt python bindings using sip"
)

add_custom_command ( OUTPUT ${SIP_SRC}
                     COMMAND ${CMAKE_COMMAND} ARGS -E copy_if_different ${SIP_SRC_IN} ${SIP_SRC}
                     DEPENDS ${SIP_SRC_AUTO}
                     COMMENT ""
)                     

# Needed for sip.h header that can end up in a different place to to the main Python include directory
include_directories ( SYSTEM ${SIP_INCLUDE_DIR} )

# Needed for sip generated files to find includes in src
include_directories ( ${CMAKE_CURRENT_SOURCE_DIR} )

# Other folders that need to be included...
include_directories ( ../SliceViewer/inc )
include_directories ( ../MantidWidgets/inc )
include_directories ( ../MantidWidgets/inc/MantidQtMantidWidgets )
include_directories ( ../MantidWidgets/inc/MantidQtMantidWidgets/InstrumentView )
include_directories ( ../Factory/inc )
include_directories ( ../SpectrumViewer/inc )
include_directories ( ../RefDetectorViewer/inc )
include_directories ( ${CMAKE_CURRENT_BINARY_DIR}/../SliceViewer ) # to find the ui_*.h auto-generated files
include_directories ( ${CMAKE_CURRENT_BINARY_DIR}/../RefDetectorViewer ) # to find the ui_*.h auto-generated files

# This creates the target library, just for python bindings
add_library ( mantidqtpython MODULE ${SIP_SRC} )

if(WIN32)
  # Windows: Python library name needs to end in .pyd for Windows
  set_target_properties( mantidqtpython PROPERTIES PREFIX "" SUFFIX ".pyd" )
  # Debug python library expects imported module names to end in _d
  if ( PYTHON_DEBUG_LIBRARY )
    set_target_properties ( mantidqtpython PROPERTIES DEBUG_OUTPUT_NAME mantidqtpython_d )
  endif ()
  # For a debug build copy the special PyQt4 debug build to the bin directory
  add_custom_command (
    TARGET mantidqtpython
    POST_BUILD
    COMMAND if 1==$<CONFIG:Debug> ${CMAKE_COMMAND} 
      -E copy_directory ${PYTHON_DIR}/msvc-site-packages/debug/PyQt4 ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${CMAKE_CFG_INTDIR}/PyQt4
    COMMENT "Copying debug PyQt4 to bin"
  )
    
elseif ( APPLE )
	# Mac: and in .so on the Mac, with no "lib" prefix either
  	set_target_properties ( mantidqtpython PROPERTIES PREFIX "" SUFFIX .so )

if (OSX_VERSION VERSION_GREATER 10.8)
  set_target_properties ( mantidqtpython PROPERTIES INSTALL_RPATH "@loader_path/../MacOS")
endif () 

else ()
	# Linux: needs to NOT have the usual "lib" prefix. 
	set_target_properties( mantidqtpython PROPERTIES PREFIX "" )
endif ()

# ... and links to other required libs ...
target_link_libraries ( mantidqtpython LINK_PRIVATE ${TCMALLOC_LIBRARIES_LINKTIME} 
            MantidQtAPI
            MantidQtSliceViewer
            MantidQtFactory
            MantidQtRefDetectorViewer
            MantidWidgets
            ${CORE_MANTIDLIBS}
            ${POCO_LIBRARIES}
            ${Boost_LIBRARIES}
            ${QT_LIBRARIES}
            ${QWT_LIBRARIES}
            ${PYTHON_LIBRARIES}
            )


###########################################################################
# Installation settings
###########################################################################

install ( TARGETS mantidqtpython DESTINATION ${BIN_DIR} )

