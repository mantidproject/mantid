# - enable_unity_build
# based upon http://cheind.wordpress.com/2009/12/10/reducing-compilation-time-unity-builds/ example. Expanded to allow the user to specify the maximum number of cpp files in each unity build file. Script then handles generation of n unity build files.

function(enable_unity_build UB_SUFFIX SOURCE_VARIABLE_NAME SOURCE_UNITY_IGNORE UNIT_SIZE)
  #limit is zero based conversion of unit_size
  math(EXPR LIMIT ${UNIT_SIZE}-1)
  set(files ${${SOURCE_VARIABLE_NAME}})

  #counts the number of cpp files up to the threshold
  set(counter ${LIMIT})

  #Have one or more unity build files
  set(file_number 0 )
  set(unit_build_files, "")
  foreach(source_file ${files} )
    if(counter EQUAL LIMIT)
      set(unit_build_file ${CMAKE_CURRENT_BINARY_DIR}/ub_${file_number}_${UB_SUFFIX}.cpp)
      # Open the ub file
      FILE(WRITE ${unit_build_file} "// Unity Build generated by CMake\n")
      # Keep running record of all unity build files
      set(unit_build_files ${unit_build_files} ${unit_build_file})
      # Increment the unity build file count
      math(EXPR file_number ${file_number}+1)
      # Reset the cpp file counter
      set(counter 0)
  else(counter EQUAL LIMIT)
    #keep counting up to the threshold. increment counter.
    math(EXPR counter ${counter}+1)
  endif(counter EQUAL LIMIT)

  set(ignore_source "true")
  foreach(ignore_file ${${SOURCE_UNITY_IGNORE}})
    if(${source_file} MATCHES ${ignore_file})
      MESSAGE(STATUS "unity build exempt ... ${source_file}")
      set(ignore_source "false")
    endif(${source_file} MATCHES ${ignore_file})
  endforeach(ignore_file)

  if("true" STREQUAL ${ignore_source})
    #Effectivly ignore the cpp files from the build.
    set_source_files_properties(${source_file} PROPERTIES HEADER_FILE_ONLY true)
    FILE( APPEND ${unit_build_file} "#include <${CMAKE_CURRENT_SOURCE_DIR}/${source_file}>\n")
  endif("true" STREQUAL ${ignore_source})

  endforeach(source_file)
  set(${SOURCE_VARIABLE_NAME} ${${SOURCE_VARIABLE_NAME}} ${unit_build_files} PARENT_SCOPE)
endfunction(enable_unity_build)



