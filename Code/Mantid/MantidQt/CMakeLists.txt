
###########################################################################
# Add OpenMP flags here
###########################################################################
if ( OPENMP_FOUND )
  set (CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${OpenMP_C_FLAGS}" )
  set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}" )
endif ()

###########################################################################
# Now add the packages one-by-one, building up the dependencies as we go
###########################################################################

include_directories ( SYSTEM ${QWT_INCLUDE_DIR} )
include_directories ( API/inc )
add_subdirectory ( API )
add_subdirectory ( MantidWidgets )
add_subdirectory ( CustomDialogs )
add_subdirectory ( CustomInterfaces )
add_subdirectory ( DesignerPlugins )
add_subdirectory ( SliceViewer )
add_subdirectory ( ImageViewer )
add_subdirectory ( Factory )


###########################################################################
# Add a custom target to build all of the MantidQt packages
###########################################################################

if ( MSVC_IDE )
  # Add to the 'MantidQt' group in VS
  set_property ( TARGET MantidQtAPI PROPERTY FOLDER "MantidQt" )
  set_property ( TARGET MantidWidgets PROPERTY FOLDER "MantidQt" )
  set_property ( TARGET CustomDialogs PROPERTY FOLDER "MantidQt" )
  set_property ( TARGET CustomInterfaces PROPERTY FOLDER "MantidQt" )
  set_property ( TARGET DesignerPlugins PROPERTY FOLDER "MantidQt" )
  set_property ( TARGET MantidQtSliceViewer PROPERTY FOLDER "MantidQt" )
  set_property ( TARGET MantidQtImageViewer PROPERTY FOLDER "MantidQt" )
else ()
  add_custom_target ( MantidQt DEPENDS MantidQtAPI MantidWidgets 
                                       CustomDialogs CustomInterfaces
MantidQtSliceViewer MantidQtImageViewer
  )
endif ()





###########################################################################
# Do the sip generation, for python bindings
###########################################################################

find_package ( PyQt4 REQUIRED )
find_package ( SIP REQUIRED )
separate_arguments ( PYQT4_SIP_FLAGS )

if ( ${SIP_VERSION} MATCHES "04[0-9][a-f].*" )
	# SIP version is at least 4.10 (040a or higher in hex)
	# message ( STATUS "Sip version is ${SIP_VERSION}. Using mantidqt sip with docstring." )
	set ( SIP_SPEC ${CMAKE_CURRENT_SOURCE_DIR}/mantidqt.sip )
else ()
	# Pre 4.0 sip version (RHEL5 build). 
	message ( STATUS "Sip version is ${SIP_VERSION}. Using mantidqt sip without docstring." )
	set ( SIP_SPEC ${CMAKE_CURRENT_SOURCE_DIR}/mantidqt.rhel5.sip )
endif ()

set ( SIP_SRC_IN ${CMAKE_CURRENT_SOURCE_DIR}/sip_mantidqt.cpp.in )
set ( SIP_SRC ${CMAKE_CURRENT_BINARY_DIR}/sip_mantidqt.cpp )
set ( SIP_SRC_AUTO siplibmantidpythonqtpart0.cpp )

# We need to manually add all the headers that are in the sip file
# so that the dependencies are known to CMake
set ( SIP_HDRS 
	SliceViewer/inc/MantidQtSliceViewer/SliceViewer.h
)

set( SRC_UNITY_IGNORE_FILES )

###########################################################################
# Sip generated files
###########################################################################

# The code generated by sip causes compiler warnings therefore the 
# generated file is wrapped by ${SIP_SRC} and these warnings are
# disabled. In order for VS2010 to to this correctly the second
# custom command below is required

# Flags used: 
# -e : C++ exceptions turn into python exceptions.
# -j1 : split into 1 file
# -w : enable warnings
# -o : automatic docstrings (not supported in old version < 4.10)

add_custom_command ( OUTPUT ${SIP_SRC_AUTO}
                     COMMAND ${SIP_EXECUTABLE}
                          -I ${PYQT4_SIP_DIR} ${PYQT4_SIP_FLAGS}
                          -c ${CMAKE_CURRENT_BINARY_DIR} -j1 -w 
                          -e 
                          ${SIP_SPEC}
                     DEPENDS mantidqt.sip ${SIP_HDRS}
                     COMMENT "Generating mantidqt python bindings using sip"
)

add_custom_command ( OUTPUT ${SIP_SRC}
                     COMMAND ${CMAKE_COMMAND} ARGS -E copy_if_different ${SIP_SRC_IN} ${SIP_SRC}
                     DEPENDS ${SIP_SRC_AUTO}
                     COMMENT "Generating mantidqt sip wrapper"
)                     

# Needed for sip.h header that can end up in a different place to to the main Python include directory
include_directories ( ${SIP_INCLUDE_DIR} )

# Needed for sip generated files to find includes in src
include_directories ( ${CMAKE_CURRENT_SOURCE_DIR} )

# Other folders that need to be included...
include_directories ( ${CMAKE_CURRENT_SOURCE_DIR}/SliceViewer/inc )
include_directories ( ${CMAKE_CURRENT_SOURCE_DIR}/MantidWidgets/inc )
include_directories ( ${CMAKE_CURRENT_BINARY_DIR}/SliceViewer ) # to find the ui_*.h auto-generated files

include_directories ( ${CMAKE_CURRENT_SOURCE_DIR}/ImageViewer/inc )
include_directories ( ${CMAKE_CURRENT_SOURCE_DIR}/ImageViewer ) #to find ui*.h

# This creates the target library, just for python bindings
add_library ( mantidqtpython MODULE ${SIP_SRC} )

if(WIN32)
	# Windows: Python library name needs to end in .pyd for Windows
	set_target_properties( mantidqtpython PROPERTIES PREFIX "" SUFFIX ".pyd" )
elseif ( APPLE )
	# Mac: and in .so on the Mac, with no "lib" prefix either
  	set_target_properties ( mantidqtpython PROPERTIES PREFIX "" SUFFIX .so )
else ()
	# Linux: needs to NOT have the usual "lib" prefix. 
	set_target_properties( mantidqtpython PROPERTIES PREFIX "" )
endif ()

# ... and links to other required libs ...
target_link_libraries ( mantidqtpython 
	MantidQtAPI QtPropertyBrowser MantidQtSliceViewer MantidQtFactory
	${QT_LIBRARIES} ${QWT_LIBRARIES}
	${PYTHON_LIBRARIES}
	)



###########################################################################
# Installation settings
###########################################################################

install ( TARGETS mantidqtpython DESTINATION ${LIB_DIR} )

