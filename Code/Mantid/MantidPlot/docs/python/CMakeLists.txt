###########################################################################
#
# Python Sphinx documentation
#
###########################################################################
find_package ( Sphinx )

if ( SPHINX_FOUND )
  # Fill in the config file and autogen file with build information
  configure_file ( source/conf.py.in source/conf.py @ONLY ) 
  configure_file ( autogen_api.py.in autogen_api.py @ONLY )
  
  # Two builders are required
  set ( TARGET_NAME sphinx )
  set ( SPHINX_BUILD ${CMAKE_CURRENT_BINARY_DIR}/../../../${TARGET_NAME} )
  set ( BUILDER html )
  configure_file ( runsphinx.py.in runsphinx_html.py @ONLY )
  set ( BUILDER latex ) 
  configure_file ( runsphinx.py.in runsphinx_latex.py @ONLY )

  
  # Sphinx seems to require the config file to be next to the rest of the source 
  # files meaning we need to copy everything over to the build directory
  set ( SPHINX_SOURCE
    source/index.rst
    source/changes.rst
    source/contents.rst
    source/techiniques/calibration.rst
    source/api/mantid.rst
    source/api/mantidplot.rst
    source/_templates/indexcontent.html
    source/_templates/indexsidebar.html
    source/_templates/layout.html
    source/images/calibrateB2Window.png
    source/images/calibratedCurrentMerlin.png
    source/images/calibratedMantidMerlin.png
    source/images/calibrateExtendMarginAndExpectedValue.png
    source/images/calibratePlotFittedData.png
    source/images/outputOfMinimalInput.png
    source/images/plotingPeaksDifference.png
  )
  # The destination for the copied files
  set ( SPHINX_CONF_DIR ${CMAKE_CURRENT_BINARY_DIR} )
  set ( SPHINX_CONF_FILES )
  foreach( file ${SPHINX_SOURCE} )
    set ( out_file ${SPHINX_CONF_DIR}/${file} )
    add_custom_command ( OUTPUT ${out_file}
      COMMAND ${CMAKE_COMMAND} ARGS -E copy_if_different ${CMAKE_CURRENT_SOURCE_DIR}/${file} ${out_file}
      DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/${file}
    )
  set ( SPHINX_CONF_FILES ${SPHINX_CONF_FILES} ${out_file} )
  endforeach()  



  # Both depend on the autogenerated API files
  add_custom_target( ${TARGET_NAME}-generateapi
    COMMAND ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/MantidPlot -xq autogen_api.py
    DEPENDS ${SPHINX_CONF_FILES}
    COMMENT "Generating Python API for sphinx"
  )

  # HTML target
  set ( SPHINX_HTML_BUILD ${SPHINX_BUILD}/html )
  add_custom_target ( ${TARGET_NAME}-html 
    COMMAND ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/MantidPlot -xq runsphinx_html.py
    COMMENT "Build Sphinx html documentation"
  )
  add_dependencies( ${TARGET_NAME}-html ${TARGET_NAME}-generateapi )

  # Latex target
  set ( SPHINX_LATEX_BUILD ${SPHINX_BUILD}/latex )
  add_custom_target ( ${TARGET_NAME}-latex 
    COMMAND ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/MantidPlot -xq runsphinx_latex.py
    COMMENT "Build Python Sphinx latex documentation"
  )
  add_dependencies( ${TARGET_NAME}-latex ${TARGET_NAME}-generateapi )

  

endif ()
